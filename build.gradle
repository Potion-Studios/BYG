plugins {
    id 'java'
    id 'com.matyrobbrt.mc.registrationutils' version '0.2.3'
}

registrationUtils {
    group 'potionstudios.byg.reg'
    projects {
        Common { type 'common'; project ':Common' }
        Fabric { type 'fabric'; project ':Fabric' }
        Forge { type 'forge'; project ':Forge' }
    }
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    dependencies {
        compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
        annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    }

    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven { url = 'https://maven.minecraftforge.net' }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }
}