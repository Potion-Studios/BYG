plugins {
    id('java')
    id('org.spongepowered.gradle.vanilla') version '0.2.1-SNAPSHOT'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

minecraft {
    accessWideners(project.file("src/main/resources/byg.aw"))
    version(minecraft_version)
    runs {
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {

            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.4'
    compileOnly("com.google.code.findbugs:jsr305:3.0.2")
    compileOnly("com.electronwill.night-config:toml:${nightconfig_version}")
    compileOnly("blue.endless:jankson:${jankson_version}")

}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

tasks.withType(Javadoc) {
    failOnError false
}