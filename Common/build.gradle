import com.matyrobbrt.gradle.pngoptimiser.task.OptimisePNGTask

plugins {
    id('java')
    id('org.spongepowered.gradle.vanilla') version '0.2.1-SNAPSHOT'
    id('com.matyrobbrt.pngoptimiser') version '0.2.0'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

tasks.register('optimise', OptimisePNGTask) {
    file(fileTree(dir: sourceSets.main.resources.srcDirs[0], includes: ['**/*.png']))
    option 'i', 0
}

minecraft {
    accessWideners(project.file("src/main/resources/byg.aw"))
    version(minecraft_version)
    runs {
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {

            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly("org.ow2.asm:asm-tree:9.5")
    compileOnly("software.bernie.geckolib:geckolib-forge-1.19.3:${geckolib_version}")
    compileOnly("maven.modrinth:corgilib:1.19.3-${corgilib_version}-forge")
    compileOnly("com.github.glitchfiend:TerraBlender-common:${minecraft_version}-${terrablender_version}")
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
    exclude '.cache/**'

    exclude("data/forge/tags/**")
}

tasks.withType(Javadoc).configureEach {
    failOnError false
}